apply plugin: 'com.android.application'

def actionsDir = "${projectDir}/src/androidTest/java/sh/calaba/instrumentationbackend/actions"
def assetsDir = "${buildDir}/assets"
def versionFilePath = "${actionsDir}/version/Version.java"
def actionsFile = "${assetsDir}/actions"
def exampleManifestFile = "${projectDir}/../AndroidManifest.xml"
def calabashJs = "${projectDir}/../calabash-js/src/calabash.js"

android {
    compileSdkVersion 24
    defaultConfig {
        applicationId "sh.calaba.instrumentationbackend"
        //noinspection MinSdkTooLow
        minSdkVersion 8
        //noinspection OldTargetApi
        targetSdkVersion 24
        versionCode 4
        versionName "0.4.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    sourceSets {
        androidTestDebug {
            assets.srcDirs += (new File(actionsFile)).getParentFile()
        }
    }
}

task verifyInputParameters {
    doLast {
        if (!project.hasProperty('version')) {
            throw new GradleException('"version" parameter should be provided!')
        }

        println("Version: ${version}")
        println("Build started...")
    }
}

task replaceVersion {
    doLast {
        File versionFile = new File("${versionFilePath}")
        String content = versionFile.text
        content = content.replaceAll("VERSION=\"[\\d.]+\"", "VERSION=\"${version}\"")
        versionFile.write(content)
    }
}

task generateActionsList {
    doLast {
        FileTree files = fileTree(actionsDir, {
            include '**/*.java'
        })

        /*
         * Transform action path, e.g. change .../java/sh/calaba/instrumentationbackend/actions/Action.java
         * to sh.calaba.instrumentationbackend.actions.Action
         */
        String regex = ".+?(java/sh/calaba/instrumentationbackend/actions/)"
        def actions = files.collect {
            file -> def actionPath = file.getPath(); actionPath.replaceAll(regex, "sh.calaba.instrumentationbackend.actions.").replaceAll(".java", "").replaceAll("/", ".")
        }

        File outputFile = new File(actionsFile)
        File outputDirectory = outputFile.getParentFile()
        outputDirectory.mkdirs()
        outputFile.createNewFile()

        outputFile.withWriter {
            writer ->
                actions.each {
                    action -> writer.writeLine action
                    println(action)
                }
        }
    }
}

task prepareAssets(type: Copy) {
    from "${exampleManifestFile}", "${calabashJs}"
    into "${assetsDir}"
}

afterEvaluate {
    /*
     * Task order: verifyInputParameters -> replaceVersion -> generateActionsList -> prepareAssets -> compileDebugAndroidTestJavaWithJavac
     */
    replaceVersion.dependsOn(verifyInputParameters)
    generateActionsList.dependsOn(replaceVersion)
    prepareAssets.dependsOn(generateActionsList)
    compileDebugAndroidTestJavaWithJavac.dependsOn(prepareAssets)
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation 'net.sourceforge.jadex:jadex-android-antlr:2.2'
    androidTestImplementation 'com.jayway.android.robotium:robotium-solo:4.3.1'
    androidTestImplementation 'com.android.support.test:runner:0.4'
}
