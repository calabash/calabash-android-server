apply plugin: 'com.android.application'

def actionsDir = "${projectDir}/src/androidTest/java/sh/calaba/instrumentationbackend/actions"
def assetsDir = "${buildDir}/assets"
def actionsFile = "${assetsDir}/actions"
def exampleManifestFile = "${projectDir}/../AndroidManifest.xml.template"
def calabashJs = "${projectDir}/../calabash-js/src/calabash.js"
def outputPath = "${buildDir}/outputs/apk/androidTest/debug/"
def apkName = "app-debug-androidTest.apk"
def serverApkName = "TestServer.apk"
def versionFilePath = "${projectDir}/../../version"
def version = new File(versionFilePath).text.replaceAll("\\s","")
def patchVersion = version.split("\\.")[2].toInteger()

android {

    // setting this > 23, results in build errors:
    // package android.test does not exist
    compileSdkVersion 23
    defaultConfig {
        applicationId "sh.calaba.instrumentationbackend"
        //noinspection MinSdkTooLow
        minSdkVersion 8
        // Keep this sync'd with compileSdkVersion
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 23
        versionCode patchVersion
        versionName "${version}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    sourceSets {
        androidTestDebug {
            assets.srcDirs += assetsDir
        }
    }
}

task addFilesToAssets {
    doLast {
        FileTree files = fileTree(actionsDir, {
            include '**/*.java'
        })

        /*
         * Transform action path, e.g. change ../java/sh/calaba/instrumentationbackend/actions/Action.java
         * to sh.calaba.instrumentationbackend.actions.Action
         */
        String regex = ".+?(java/sh/calaba/instrumentationbackend/actions/)"
        def actions = files.collect {
            file -> def actionPath = file.getPath(); actionPath.replaceAll(regex, "sh.calaba.instrumentationbackend.actions.").replaceAll(".java", "").replaceAll("/", ".")
        }

        File outputFile = new File(actionsFile)
        File outputDirectory = outputFile.getParentFile()
        outputDirectory.mkdirs()
        outputFile.createNewFile()

        println("=== Generating Action List ===")
        outputFile.withWriter {
            writer ->
                actions.each {
                    action -> writer.writeLine action
                    println(action)
                }
        }

        println("=== Adding Additional Assets ===")

        println("Adding ${exampleManifestFile}")
        def manifestContents = new File(exampleManifestFile).text
        manifestContents = manifestContents.replaceAll("TEST_SERVER_VERSION_CODE", "${patchVersion}")
        manifestContents = manifestContents.replaceAll("TEST_SERVER_VERSION_NAME", version)
        outputFile = new File("${assetsDir}/AndroidManifest.xml")
        outputFile.createNewFile()
        outputFile.write(manifestContents)


        println("Adding ${calabashJs}")
        exec {
            commandLine "cp", "${calabashJs}", "${assetsDir}"
        }
        println("Adding ${versionFilePath}")
        exec {
            commandLine "cp", "${versionFilePath}", "${assetsDir}"
        }
    }
}

task prepareApk {
    doLast {
        def apk = "${outputPath}${apkName}"
        def server = "${outputPath}${serverApkName}"

        println("Stripping signature and manifest from server apk...")

        exec {
            commandLine "zip", "-q", "-d", "${apk}", "META-INF/*"
        }
        exec {
            commandLine "zip", "-q", "-d", "${apk}", "AndroidManifest.xml"
        }

        exec {
            commandLine "rm", "-f", "${server}"
        }

        exec {
            commandLine "cp", "${apk}", "${server}"
        }


        println("Done building.")
        println("Verifying the apk...")


        def tmpDir = new File("../../tmp/verify-apk")
        tmpDir.delete()
        tmpDir.mkdirs()

        ant.unzip(src: "${server}", dest: tmpDir.absolutePath)
        def files = []

        tmpDir.traverse(type: groovy.io.FileType.FILES) { files.add(it.path) }

        def counter = 0
        println "=== Files in APK =="
        files.each {
            println "${it}"
            counter = counter + 1
        }
        println "=== END =="

        if (counter != 10) {
            def message = "Expected 10 files in ${serverApkName} - found ${files.count}"
            throw new GradleException(message)
        }

        ["resources.arsc",
         "junit/runner/logo.gif",
         "junit/runner/smalllogo.gif",
         "classes.dex",
         "LICENSE-junit.txt",
         "assets/calabash.js",
         "assets/AndroidManifest.xml",
         "assets/version",
         "assets/actions",
         "com/jayway/android/robotium/solo/RobotiumWeb.js"].collect {
            "${tmpDir.path}/${it}".toString()
        }.each {
            if (!files.contains(it)) {
                println("${it.class}")
                throw new GradleException("Expected to find ${it} in apk")
            }
        }

        def manifestContents = new File("${tmpDir.path}/assets/AndroidManifest.xml").text
        if (!manifestContents.contains("android:versionCode=\"${patchVersion}\"")) {
            throw new GradleException("Expected to find correct android:versionCode (${patchVersion}) in AndroidManifest.xml")
        }

        if (!manifestContents.contains("android:versionName=\"${version}\"")) {
            throw new GradleException("Expected to find correct android:versionName (${version}) in AndroidManifest.xml")
        }


        println("Done verifying!")

        exec {
            commandLine "cp", "${server}", "../.."
        }

        println("Staged a copy of TestServer.apk to the root directory of this repository")

        exec {
            commandLine "cp", "${assetsDir}/AndroidManifest.xml", "../.."
        }
        println("Staged a copy of AndroidManifest.xml to the root directory of this repository")
    }
}

tasks.matching { task ->
    task.name.startsWith('compile')
}.all { task ->
    task.dependsOn(["addFilesToAssets"])
}

assembleAndroidTest.finalizedBy(prepareApk)

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation 'net.sourceforge.jadex:jadex-android-antlr:2.2'
    androidTestImplementation 'com.jayway.android.robotium:robotium-solo:4.3.1'

    //noinspection GradleDependency
    androidTestImplementation 'com.android.support.test:runner:0.4'
}
